---
title: ã€Œ47ã€GMP mainå¯åŠ¨
date: 2021/05/05 21:32:25
updated: '2021/05/05 21:33:17'
keywords: 'Go,GPM,G0,M0'
tags:
  - GPM
  - Day
  - Go
  - Goæºç 
mathjax: true
---


ä¹‹å‰æ‰¯è¿‡GPMçš„[g0å’Œm0å…³ç³»](https://blog.imrcrab.com/archives/392d66f0.html)å’Œ[mainå‡½æ•°å…¥å£](https://blog.imrcrab.com/archives/9bb71eca.html)çš„ä¸€äº›ç›¸å…³ä¿¡æ¯ã€‚


å¤šæ¬¡çœ‹go mianå‡½æ•°çš„å¯åŠ¨æµç¨‹ï¼Œå¤šæ¬¡åˆ†æï¼Œå¤šæ¬¡ç†è§£ï¼Œå…³æ³¨ç‚¹å’Œç»“è®ºå„æœ‰ä¸åŒï¼Œä»¥æ­¤è®°å½•ã€‚



### [ğŸ‘‹å…¥å£](https://github.com/golang/go/blob/release-branch.go1.14/src/runtime/asm_amd64.s#L22)ï¼š

![](https://crab-1251738482.cos.ap-guangzhou.myqcloud.com/clipboard_20210505_094951.png)

<!--more-->

### rt0_goå‡½æ•°ï¼š

```go
TEXT runtimeÂ·rt0_go(SB),NOSPLIT|TOPFRAME,$0
	// copy arguments forward on an even stack
	MOVQ	DI, AX		// argc
	MOVQ	SI, BX		// argv
	SUBQ	$(4*8+7), SP		// 2args 2auto
	ANDQ	$~15, SP
	MOVQ	AX, 16(SP)
	MOVQ	BX, 24(SP)

    // åˆ›å»ºæ“ä½œç³»ç»Ÿstack
	// create istack out of the given (operating system) stack.
	// _cgo_init may update stackguard.
	MOVQ	$runtimeÂ·g0(SB), DI
	LEAQ	(-64*1024+104)(SP), BX
	MOVQ	BX, g_stackguard0(DI)
	MOVQ	BX, g_stackguard1(DI)
	MOVQ	BX, (g_stack+stack_lo)(DI)
	MOVQ	SP, (g_stack+stack_hi)(DI)

	// find out information about the processor we're on
	MOVL	$0, AX
	CPUID
	MOVL	AX, SI
	CMPL	AX, $0
	JE	nocpuinfo

    // åºåˆ—åŒ–é€‰æ‹©
	// Figure out how to serialize RDTSC.
	// On Intel processors LFENCE is enough. AMD requires MFENCE.
	// Don't know about the rest, so let's do MFENCE.
	CMPL	BX, $0x756E6547  // "Genu"
	JNE	notintel
	CMPL	DX, $0x49656E69  // "ineI"
	JNE	notintel
	CMPL	CX, $0x6C65746E  // "ntel"
	JNE	notintel
	MOVB	$1, runtimeÂ·isIntel(SB)
	MOVB	$1, runtimeÂ·lfenceBeforeRdtsc(SB)
notintel:

	// Load EAX=1 cpuid flags
	MOVL	$1, AX
	CPUID
	MOVL	AX, runtimeÂ·processorVersionInfo(SB)

nocpuinfo:
	// if there is an _cgo_init, call it.
	MOVQ	_cgo_init(SB), AX
	TESTQ	AX, AX
	JZ	needtls
	// arg 1: g0, already in DI
	MOVQ	$setg_gcc<>(SB), SI // arg 2: setg_gcc
#ifdef GOOS_android
	MOVQ	$runtimeÂ·tls_g(SB), DX 	// arg 3: &tls_g
	// arg 4: TLS base, stored in slot 0 (Android's TLS_SLOT_SELF).
	// Compensate for tls_g (+16).
	MOVQ	-16(TLS), CX
#else
	MOVQ	$0, DX	// arg 3, 4: not used when using platform's TLS
	MOVQ	$0, CX
#endif
#ifdef GOOS_windows
	// Adjust for the Win64 calling convention.
	MOVQ	CX, R9 // arg 4
	MOVQ	DX, R8 // arg 3
	MOVQ	SI, DX // arg 2
	MOVQ	DI, CX // arg 1
#endif
	CALL	AX

	// update stackguard after _cgo_init
	MOVQ	$runtimeÂ·g0(SB), CX
	MOVQ	(g_stack+stack_lo)(CX), AX
	ADDQ	$const__StackGuard, AX
	MOVQ	AX, g_stackguard0(CX)
	MOVQ	AX, g_stackguard1(CX)

#ifndef GOOS_windows
	JMP ok
#endif
needtls:
#ifdef GOOS_plan9
	// skip TLS setup on Plan 9
	JMP ok
#endif
#ifdef GOOS_solaris
	// skip TLS setup on Solaris
	JMP ok
#endif
#ifdef GOOS_illumos
	// skip TLS setup on illumos
	JMP ok
#endif
#ifdef GOOS_darwin
	// skip TLS setup on Darwin
	JMP ok
#endif
#ifdef GOOS_openbsd
	// skip TLS setup on OpenBSD
	JMP ok
#endif

	LEAQ	runtimeÂ·m0+m_tls(SB), DI
	CALL	runtimeÂ·settls(SB)

    // tlsè®¾ç½®
	// store through it, to make sure it works
	get_tls(BX)
	MOVQ	$0x123, g(BX)
	MOVQ	runtimeÂ·m0+m_tls(SB), AX
	CMPQ	AX, $0x123
	JEQ 2(PC)
	CALL	runtimeÂ·abort(SB)
ok:
	// set the per-goroutine and per-mach "registers"
	get_tls(BX)
	LEAQ	runtimeÂ·g0(SB), CX
	MOVQ	CX, g(BX)
	LEAQ	runtimeÂ·m0(SB), AX

    // m.g0ç»‘å®šä¸€ä¸ªg0
	// save m->g0 = g0
	MOVQ	CX, m_g0(AX)
    // g0ç»‘å®šm0
	// save m0 to g0->m
	MOVQ	AX, g_m(CX)

	CLD				// convention is D is always left cleared
	CALL	runtimeÂ·check(SB)

	MOVL	16(SP), AX		// copy argc
	MOVL	AX, 0(SP)
	MOVQ	24(SP), AX		// copy argv
	MOVQ	AX, 8(SP)
    // å‚æ•°åˆå§‹åŒ–
	CALL	runtimeÂ·args(SB)
    // ç³»ç»Ÿåˆå§‹åŒ–
	CALL	runtimeÂ·osinit(SB)
    // è°ƒåº¦ä»»åŠ¡åˆå§‹åŒ–ï¼Œå…·ä½“é€»è¾‘ä¸‹é¢æœ‰æåˆ°
	CALL	runtimeÂ·schedinit(SB)

    // åˆ›å»ºç¬¬ä¸€ä¸ªgï¼Œç”¨äºå¯åŠ¨ç¨‹åº
	// create a new goroutine to start program
	MOVQ	$runtimeÂ·mainPC(SB), AX		// entry
	PUSHQ	AX
	PUSHQ	$0			// arg size
	CALL	runtimeÂ·newproc(SB)
	POPQ	AX
	POPQ	AX

	// start this M
    //  å¯åŠ¨M
	CALL	runtimeÂ·mstart(SB)

	CALL	runtimeÂ·abort(SB)	// mstart should never return
	RET

	// Prevent dead-code elimination of debugCallV2, which is
	// intended to be called by debuggers.
	MOVQ	$runtimeÂ·debugCallV2<ABIInternal>(SB), AX
	RET

```

### â¡ï¸â¡ï¸[ğŸ‘‹mstartä½œç”¨](https://blog.imrcrab.com/archives/392d66f0.html#mstart)

### å¾…æ›´æ–°...